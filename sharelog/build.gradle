apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

buildscript {
    ext.libVersionCode = 1
    ext.libVersionName = "1.0"
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

bintray {
    user=BINTRAY_USER
    key=BINTRAY_KEY
    publications = ['SharelogPublication']
    pkg {
        repo = 'android'
        name = 'sharelog'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/dawidfiruzek/sharelog.git'
        version {
            name = libVersionName
            released  = new Date()
        }
    }
}

publishing {
    publications {
        SharelogPublication(MavenPublication) {
            groupId 'pl.dawidfiruzek'
            artifactId 'sharelog'
            version libVersionName
            pom.withXml {
                // Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.compile.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}

repositories {
    mavenCentral()
    maven {
        url "https://maven.google.com"
    }
}

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 26
        versionCode libVersionCode
        versionName libVersionName
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.10.0'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    compile 'com.android.support:support-v4:26.0.1'
}
